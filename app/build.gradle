plugins {
    id 'com.android.application'

    // http://errorprone.info/
    // https://plugins.gradle.org/plugin/net.ltgt.errorprone
    // https://github.com/tbroyer/gradle-errorprone-plugin
    id 'net.ltgt.errorprone' version '0.0.13'

    // https://www.fabric.io/
    // https://fabric.io/
    // https://fabric.io/kits/android/crashlytics/install
    id 'io.fabric'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        step2hell {
            storeFile file('step2hell.jks')
            storePassword STORE_PASSWORD
            keyAlias 'step2hell'
            keyPassword KEY_PASSWORD
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [giphyKey: "Logw8R6FG2xKjxihEuq7GDeif7dDBawc"] // This is a Giphy API key, use this key while develop application and experiment with integrations.
            minifyEnabled false
        }
        release {
            manifestPlaceholders = [giphyKey: "Logw8R6FG2xKjxihEuq7GDeif7dDBawc"] // Todo: request a production key.
            signingConfig signingConfigs.step2hell
            zipAlignEnabled true
            minifyEnabled false
            /**
             * 1. minifyEnabled=true was asked before shrinkResources=true
             * 2. shrinkResources does not work with jack yet.
             * 3. be careful of proguard-rules, otherwise shrinkResources will cause build failed.
             * https://stackoverflow.com/questions/39592035/cant-use-shrinkresources-res-resources-prod-release-stripped-ap-specified-fo
             * https://code.google.com/p/android/issues/detail?id=223254
             * https://stackoverflow.com/questions/36540676/build-intermediates-res-resources-anzhi-debug-stripped-ap-specified-for-prope
             * https://developer.android.com/studio/build/shrink-code.html#shrink-resources
             */
//            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

//    splits {
//        abi {
//            enable = true
//        }
//        density {
//            enable = true
//            exclude "mdpi","hdpi","xhdpi","xxxhdpi"
////            reset()
////            include "xxhdpi", "ldpi"
//        }
//        language {
//            enable = true
//            include "zh", "en"
//        }
//    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation "junit:junit:$rootProject.ext.junit"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockito"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrest"
    testImplementation "com.google.truth:truth:$rootProject.ext.truth"
    testImplementation "com.google.truth.extensions:truth-java8-extension:$rootProject.ext.truth"

    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runner"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.rules"
    androidTestImplementation "com.android.support:support-annotations:$rootProject.ext.support"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "com.android.support:appcompat-v7:$rootProject.ext.support"
    implementation "com.android.support:design:$rootProject.ext.support"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.support"
    implementation "com.android.support:cardview-v7:$rootProject.ext.support"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraint"
    implementation "com.android.support:exifinterface:$rootProject.ext.support"
    implementation "com.android.support:support-compat:$rootProject.ext.support"
    implementation "com.android.support:support-content:$rootProject.ext.support"

    implementation "org.jsoup:jsoup:$rootProject.ext.jsoup"

    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxandroid"
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofit"

    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttp"

    implementation "com.google.dagger:dagger:$rootProject.ext.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.dagger"

    implementation "com.github.bumptech.glide:glide:$rootProject.ext.glide"
    implementation "com.github.bumptech.glide:gifdecoder:$rootProject.ext.glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glide"

    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.ext.fabric") {
        transitive = true;
    }

    implementation "pl.droidsonroids.gif:android-gif-drawable:$rootProject.ext.gifDrawable"
    implementation("com.giphy.sdk:core:$rootProject.ext.giphy") {
        transitive = true
    }

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.ext.leakcanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.leakcanary"

    debugImplementation "com.github.markzhai:blockcanary-android:$rootProject.ext.blockcanary"
    releaseImplementation "com.github.markzhai:blockcanary-android:$rootProject.ext.blockcanary"

}

apply from: "${rootDir}/gradle/launch.gradle"